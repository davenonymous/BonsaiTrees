plugins {
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'eclipse'
	id 'com.diffplug.eclipse.apt' version '3.43.0'
	id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

group = "com.davenonymous.bonsaitrees3"
version = mod_version
archivesBaseName = "${mod_name}-${mc_version}"

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: "${mappings_type}", version: "${mappings_version}"

    runs {
        client {
            workingDirectory project.file('run/client')
			
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
			
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                bonsaitrees3 {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
			
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
			
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                bonsaitrees3 {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
			
            args '--mod', 'bonsaitrees3', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                bonsaitrees3 {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven {	
		name = 'BlameJared'
		url = 'https://maven.blamejared.com/'
	}
	maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
	flatDir {
        dir 'libs'
    }
}

dependencies {
	// Minecraft
	minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
	
	// Mixins
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	// Libnonymous
	implementation fg.deobf("com.davenonymous.libnonymous:libnonymous:${libnonymous_version}")
	
	// Just Enough Items API
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")

	// The One Probe
    implementation fg.deobf("curse.maven:the-one-probe-245211:${top_fileid}")
	
	
	// Runtime Only
	
	// Just Enough Items
	runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")
	
	// Configured
    runtimeOnly fg.deobf("curse.maven:configured-457570:${configured_fileid}")
	
	// Spark
    runtimeOnly fg.deobf("curse.maven:spark-361579:${spark_fileid}")
	
	// Biomes O' Plenty
    runtimeOnly fg.deobf("curse.maven:biomes-o-plenty-220318:${bop_fileid}")
	
	// Oh The Biomes You'll Go
    runtimeOnly fg.deobf("curse.maven:oh-the-biomes-youll-go-247560:${byg_fileid}")
	
	// TerraBlender
    runtimeOnly fg.deobf("curse.maven:terrablender-563928:${terrablender_fileid}")
	
	// GeckoLib
    runtimeOnly fg.deobf("curse.maven:geckolib-388172:${geckolib_fileid}")
	
	// CorgiLib
    runtimeOnly fg.deobf("curse.maven:corgilib-693313:${corgilib_fileid}")
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "Bonsai Trees 3",
                "Specification-Vendor"    : "Davenonymous",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Davenonymous",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


import groovy.json.JsonOutput
import groovy.json.JsonSlurper

processResources {

    // Minifies all .json files when building the mod.
    // Source files are not minified, only the jar copies.
    doLast {

        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0

        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {

            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }

        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}